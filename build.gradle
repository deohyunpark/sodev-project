plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.1'
    id 'io.spring.dependency-management' version '1.1.0'
}

bootJar {
    enabled = false
}

subprojects {
    group = 'dev.be'
    version = '0.0.1-SNAPSHOT'

    java {
        sourceCompatibility = '17'
    }

    apply plugin: 'java'
    apply plugin: 'java-library' // build.gradle 에서 api() 를 사용하려면 java-library 사용
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management' // spring boot dependency 를 사용하여 사용중인 부트 버전에서 자동으로 의존성을 가져온다.

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    // 관리하는 모듈의 공통 dependencies
    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    tasks.register("prepareKotlinBuildScriptModel") {}
}

project(':sodev-api') { // 컴파일 시 sodev-common 로딩
    dependencies {
        compileOnly project(':sodev-common')
    }

    jar { enabled = false } // sodev-api -> 의존성이 전부 들어간 jar 파일만 필요하고 의존성이 없는 plain jar 파일은 필요 없다.
}

project(':sodev-common') { // sodev-common -> bootJar로 패키징 할 필요 없음
    bootJar { enabled = false }
    jar { enabled = true }
}
